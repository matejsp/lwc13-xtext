\subsection{Including Expressions into the QL Language}

Recall the example of a housowning questionnaire as mentioned in the LWC13 task:

\begin{lstlisting}[language=QL]
import types.Money

form Box1HouseOwning {
	hasSoldHouse: "Did you sell a house in 2010?" boolean
	hasBoughtHouse: "Did you by a house in 2010?" boolean
	hasMaintLoan: "Did you enter a loan for maintenance/reconstruction?" boolean
	
	if (hasSoldHouse) {
		sellingPrice: "Price the house was sold for: " Money 
		privateDebt: "Private debts for the sold house: " Money
		valueResidue: "Value residue: " Money (sellingPrice - privateDebt) 
	}
}
\end{lstlisting}

Compared to the language developed in section \ref{sec:DefiningGrammar}, we need to add (1) a condition
statement to express optional questions (see line 8) and (2) the capability for automatically 
deriving a question's answer from previously answered questions (see line 11). As explained in section
\ref{sec:DefiningGrammar}, our grammar inherits from Xbase making its rules reusable in the 
questionnaire language. To fulfill the missing requirements our grammar needs to be extended to the following:

\begin{lstlisting}[language=Xtext]
grammar org.eclipse.xtext.example.ql.QlDsl with org.eclipse.xtext.xbase.Xbase

generate qlDsl "http://www.eclipse.org/xtext/example/ql/QlDsl"

/* The top-most container of QL files is a Questionnaire */
Questionnaire:
  imports+=Import*
  forms+=Form*;

/* Allows importing of qualified names of types */
Import:
  'import' importedNamespace=QualifiedName;

/* QL consists of questions grouped in a top-level form construct. */
Form:
  "form" name=ID "{"
    element += FormElement*
  "}";

/* Abstract rule for elements contained in a Form */
FormElement:
  Question | ConditionalQuestionGroup
;

/**
 * - Each question identified by a name that at the same time represents the result of the question.
 * - A question has a label that contains the actual question text presented to the user.
 * - Every question has a type.
 * - A question can optionally be associated to an expression:
 *   this makes the question computed
 */
Question:
  name=ID ":" label=STRING type=JvmTypeReference expression=XParenthesizedExpression?
;

/**
 * Groups questions within a block, optionally made conditional with an if-condition.
 */
ConditionalQuestionGroup: {ConditionalQuestionGroup}
  ("if" condition=XParenthesizedExpression)? "{"
    element += FormElement*
  "}"
;
\end{lstlisting}

Compared to the grammar defined in section \ref{sec:DefiningGrammar}, the follwoing points have changed:


\begin{lstlisting}[language=Xtext]
FormElement:
  Question | ConditionalQuestionGroup
;
\end{lstlisting}

A \texttt{FormElement} is now either a normal question or a \texttt{ConditionalQuestionGroup}. Conditional
question groups are groups of form elements embraced by an optional \texttt{if}-condition:

\begin{lstlisting}[language=Xtext]
ConditionalQuestionGroup: {ConditionalQuestionGroup}
  ("if" condition=XParenthesizedExpression)? "{"
    element += FormElement*
  "}"
;
\end{lstlisting}

For the condition of the \texttt{if}-statement the grammar rule \texttt{XParenthesizedExpression} 
inherited from Xbase is used. An \texttt{XParenthesizedExpression} is simply an expression in parenthesis.
The \texttt{if}-statement is optional (as defined by the question mark '?' symbol) which allows for just
grouping questions without the necessarity for a condition. The inner element are again \texttt{FormElement}s,
making it possible to nest groups within groups and so on. The last part that has changed is the \texttt{Question}
rule. Here again the rule \texttt{XParenthesizedExpression} is used to optionally embed Xbase expressions:

\begin{lstlisting}[language=Xtext]
Question:
  name=ID ":" label=STRING type=JvmTypeReference expression=XParenthesizedExpression?
;
\end{lstlisting}

Xbase comes out of the box with the support for standard Java types like like Strings or Integers inside expressions .
However, in the questionnaire language own data types, like the Money type from the example, need also to be
integrated. Such data types will be typically defined in a Java class. When importing such a type via the \texttt{import}
statement, it will be available in the questionnaire definition. However, Xbase needs to know how to handle
these types when they are used in expressions with operators like '+'. '-', '*' and '/'. The logic for these
operators need to be implemented in special methods in the data type itself. As example, let's see how this
is achieved for the Money type:

\begin{lstlisting}[language=Java]
package types;

import java.math.BigDecimal;

public class Money {
	private BigDecimal amount;

	public Money (BigDecimal amount) {
		this.amount = amount;
	}
	public BigDecimal getAmount() {
		return amount;
	}

	// Implement operators
	public Money operator_minus (Money other) {
		return new Money(this.amount.subtract(other.amount));
	}
	public Money operator_plus (Money other) {
		return new Money(this.amount.add(other.amount));
	}
	public Money operator_multiply (Money other) {
		return new Money(this.amount.multiply(other.amount));
	}
	public Money operator_divide (Money other) {
		return new Money(this.amount.divide(other.amount));
	}
}
\end{lstlisting}

The data type \texttt{Money} simply holds the amount as a value of type \texttt{BigDecimal}. For each
operator a special method, e.g. \texttt{operator\_minus(Money other)}, defines how to procede when this
operator is used two values of type \texttt{Money}. In this simple example, a new Money object is created
and its value is computed corresponding to the operator type. When evaluating an expression, Xbase searches
for these methods inside the used types to compute the result.

In order to test the new version of the questionnaire language, the MWE workflow needs to be executed
again (\emph{Right click on GenerateQlDsl.mwe2 / Run As.. / MWE2 Workflow}). The questionnaire language
now supports expressions, but there is still one point missing: Questions cannot be referenced within
an expression. For this, we need to derive a JVM model from the questionnaire model which we will discuss
in the next section.