grammar org.eclipse.xtext.example.qls.QlsDsl with org.eclipse.xtext.common.Terminals

generate qlsDsl "http://www.eclipse.org/xtext/example/qls/QlsDsl"
import "http://www.eclipse.org/xtext/example/ql/QlDsl" as ql

/**
 * Following requirements should be fulfilled:
 * - grouping of related questions into pages, sections, subsection
 * - font style (italic, bold, underlined), font type, and color for labels
 * - widget selection 
 */

QuestionnaireStyleModel:
	pages+=Page*;
	
Page:
	"page" name=ID ("uses" form=[ql::Form|ID])? "{"
		element+=PageElement*
		navigation=Navigation?
	"}"
;

PageElement:
	Question | Section
;

Question:
	"question" question=[ql::Question] styling+=StyleInformation?	
;

StyleInformation: {StyleInformation}
	"["
	(
		("font-style:" fontStyle=STRING)? &
		("font-weight:" fontWeight=STRING)? &
		("font-color:" fontColor=STRING)? &
		("font-family:" fontFamily=STRING)? &
		("widget:" widget=Widget)?
	)
	"]"
;

Widget: {Widget}
	widgetType=("Radio"|"DropDown"|"CheckBox"|"Text"|"Slider") ("[" labels+=STRING ("," labels+=STRING)* "]")?
;
  
Section:
	"section" name=ID ("uses" form=[ql::Form|ID])? "{"
		element+=PageElement*
	"}"
;

Navigation: {Navigation}
	"navigation" "{" (nextPage+=NextPage)* "}"
;

NextPage:
	page=[Page|ID] // TODO enable xbase's expressions here
;